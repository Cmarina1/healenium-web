plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id "com.github.hierynomus.license" version "0.15.0"
    id "com.jfrog.bintray" version "1.8.4"
    id 'distribution'
}

group 'com.epam.healenium'
sourceCompatibility = 1.8

repositories {
    maven {
        url "https://dl.bintray.com/epam/healenium"
    }
    mavenCentral()
}

dependencies {
    compile group: 'com.typesafe', name: 'config', version: '1.3.4'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
    compile group: 'com.epam.healenium', name: 'tree-comparing', version: '0.4.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.12'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.19.v20190610'
    compile group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '2.1'
    testCompile group: 'com.codeborne', name: 'selenide', version: '5.5.1'
    testCompile group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'

}

ext {
    bintrayUserName = (project.hasProperty('BINTRAY_USERNAME') ? project.findProperty('BINTRAY_USERNAME') : System.getenv()['BINTRAY_USERNAME'])
    bintrayPassword = (project.hasProperty('BINTRAY_PASSWORD') ? project.findProperty('BINTRAY_PASSWORD') : System.getenv()['BINTRAY_PASSWORD'])
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir
    archiveClassifier = "javadoc"
}

publishing {
    publications {
        HealeniumPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom {
                name = "healenium-web"
                description = 'Self-healing library for Selenium Web-based tests'
                url = "https://github.com/healenium/healenium-web.git"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
}


bintray {
    user = bintrayUserName
    key = bintrayPassword
    publications = ['HealeniumPublication']
    dryRun = false
    publish = true
    pkg {
        userOrg = 'epam'
        repo = 'healenium'
        name = 'healenium-web'
        desc = project.description
        websiteUrl = 'https://github.com/healenium'
        issueTrackerUrl = 'https://github.com/healenium/healenium-web/issues'
        vcsUrl = 'https://github.com/healenium/healenium-web.git'
        licenses = ['Apache-2.0']
        labels = ['gradle', 'healenium']
        publicDownloadNumbers = true
        attributes = [:]
        version {
            name = project.version
            vcsTag = project.version
            attributes = [:]
        }
    }
}

bintrayUpload.onlyIf {
    !project.version.toString().toUpperCase().endsWith("-SNAPSHOT") && !project.version.toString().toUpperCase().contains("-DEV")
}


jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

license {
    header rootProject.file('HEADER')
    excludes(["**/*.html", "*.conf", "*.properties"])
    strictCheck false
}
